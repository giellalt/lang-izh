!! # Noun inflection
! ----

!! Here are the suffixes for noun inflection.

LEXICON N_YÖ-SG
! yö:yö
@U.POS.N@+Dial/Lauk+N:@U.POS.N@ N_YÖ-SG_01 ;

LEXICON N_YÖ-SG_01
 SG-NOM   ; ! yö
 SG-PAR_tA   ; ! yötä
 SG-ILL_hV  ; ! yöhö
 Oblique-Singular ;


LEXICON N_MAA
! maa (I)
+N: DER-tOin ; ! maatoin
@U.POS.N@+N:@U.POS.N@ N_MAA_01 ;

LEXICON N_MAA_01
 SG-NOM   ; ! maa
 SG-PAR_tA   ; ! maata
 SG-ILL_hV  ; ! maaha
 Oblique-Singular ;
! Plural
 PL-NOM   ; ! maat
+Pl+Gen:%>i%>jen	#   ; ! maijen
 PL-PAR_tA  ; ! maita
 PL-ILL_hV   ; ! maihe
 Oblique-Plural ;

LEXICON N_PREESENS
! preesens:preesens
@U.POS.N@+N:@U.POS.N@ N_PREESENS_01 ;

LEXICON N_PREESENS_01
 SG-NOM   ; ! preesens
:a SG-PAR_A  ; ! maita
:a SG-ILL_V   ; ! maihe
:a Oblique-Singular ;
! Plural
:a PL-NOM   ; ! maat
:o PL-PAR_tA  ; ! maita
:o PL-ILL_hV   ; ! maihe
:o Oblique-Plural ;

LEXICON N_GLASNOI
@U.POS.N@+N:@U.POS.N@ NMN_GLASNOI ;

LEXICON N_RISTI
! risti (II)
+N: DER-tOin ; ! ristitöin
@U.POS.N@+N:@U.POS.N@ N-risti_01 ;

LEXICON N-risti_01
     SG-NOM   ; ! risti
!:%>%^LCns%^LVws SG-PAR_A   ; ! mattooa !Is this right
 SG-PAR_A-LCns   ; ! mattoa
 SG-ILL_V   ; ! mattoo
 Oblique-Singular ;
! Plural
 lOi-Pl ;
: PL-PAR_A ; ! matoja
:i PL-PAR_A ; ! matoja
: PL-ILL_hV ; ! matoja
:%^NoGrad Oblique-Plural ; ! PL-PAR and PL-ILL needed 2012-10-31
:%>%^WCns PL-NOM   ; ! maot

LEXICON N_MATO
! mato (II)
+N: DER-tOin ; ! maotoin
@U.POS.N@+N:@U.POS.N@ N-mato_01 ;

LEXICON N-mato_01
     SG-NOM   ; ! mato
!:%>%^LCns%^LVws SG-PAR_A   ; ! mattooa !Is this right
 SG-PAR_A-LCns   ; ! mattoa
 SG-ILL_V   ; ! mattoo
 Oblique-Singular ;
! Plural
 lOi-Pl ;
: PL-PAR_A ; ! matoja
:%^NoGrad Oblique-Plural ; ! PL-PAR and PL-ILL needed 2012-10-31
:%>%^WCns PL-NOM   ; ! maot

LEXICON N_KOIVU
! koivu
+N: DER-tOin ; ! koivutoin
@U.POS.N@+N:@U.POS.N@ N-koivu_01 ;
@U.POS.N@+N:@U.POS.N@%^NoGrad Oblique-Plural_hV ;
@U.POS.N@+N:@U.POS.N@ PL-PAR_A ;

LEXICON N-koivu_01
   SG-NOM   ; ! koivu
 SG-PAR_A-LCns   ; ! koivua
 SG-ILL_V   ; ! koivuu
 Oblique-Singular ;
! Plural
 lOi-Pl ;
:%^NoGrad Oblique-Plural ; ! 
 PL-NOM   ; ! koivut

LEXICON N_OSA
! forma osa (II-b)
+N: DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-osa_01 ;

LEXICON N-osa_01
 SG-NOM   ; ! osa
 SG-PAR_A-LCns   ; ! ossaa
 SG-ILL_V   ; ! ossaa
 Oblique-Singular ;
! Plural
 PL-NOM   ; ! osat
Err/Sub:%>i%> PL-PAR_A   ; ! ossia
 PL-PAR_A-LCns   ; ! ossia
 PL-ILL_V   ; ! ossii
 Oblique-Plural ;

LEXICON N_JOKI
! joki:jok
! joki (II-c)
+N:e DER-tOin ; ! joetoin
@U.POS.N@+N:@U.POS.N@ N-joki_01 ;

LEXICON N-joki_01
:i  SG-NOM   ; ! joki
:e SG-PAR_A-LCns   ; ! mattooa
:e SG-ILL_V   ; ! mattoo
:e  Oblique-Singular ;
! Plural
:i lOi-Pl ;
:e       PL-NOM    ; ! joet
   PL-PAR_A-LCns   ; ! kumpia
:i       PL-PAR_A   ; ! kumpia
:       PL-PAR_A   ; ! kumpia
 Oblique-Plural_V ;

LEXICON N_LUMI
! lumi (II-d)
+N:me DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-lumi_01 ;

LEXICON N-lumi_01
+Sg+Nom:mi     #   ; ! lumi
:n SG-PAR_tA  ; ! lunta
+Sg+Ill:me%>%^LCnsV1	#   ; ! mattoo
:me  Oblique-Singular ;
! Plural
+Pl:mi lOi-Pl ;
:me PL-NOM   ; ! lumet


LEXICON N_EMOI
! emoi (III)
+N: DER-tOin ; !emotoin
@U.POS.N@+N:@U.POS.N@ N-emoi_01 ;

LEXICON N-emoi_01
+Dial/Soik+Sg+Nom:i     #   ; ! emoi
+Dial/Lauk+Sg+Nom:     #   ; ! emoi
+Sg+Par:i%>%^LCns%^LVwsAÄ1	#   ; ! emmooja
+Sg+Ill:i%>%^LCnsV1	#   ; ! emoihe
:i Oblique-Singular ;
! Plural
 lOi-Pl ;
+Pl+Nom:i%>%^WCnst     #   ; ! emoit

LEXICON N_JALKA
! the stem-final "a" is added here
! jalka (IV)
+N+Der/tOin:a%>%^WCnst A_KALATOIN ; !
@U.POS.N@+N:@U.POS.N@ N-jalka_01 ;

LEXICON N-jalka_01
:a SG-NOM   ; ! jalka
:a SG-PAR_A-LCns   ; ! jalkaa
:a SG-ILL_V   ; ! jalkaa
:a  Oblique-Singular ;
! Plural
:a       PL-NOM    ; ! jalat
:o 		 PL-PAR_A ; !  jalkoja, cf. kanoja
:o%>%^LCns	 PL-PAR_A ; ! jalkoja, cf. kannoja
+Pl+Ill:o%>i%>hV1	#   ; ! jalkoihe
:o Oblique-Plural ;

LEXICON N_KANA
! copied from N-jalka
! jalka (IV-a)
+N:a DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-kana_01 ;

LEXICON N-kana_01
:a     SG-NOM   ; ! kana
:a SG-PAR_A-LCns ;
:a SG-ILL_V  ; ! jalkaa
:a  Oblique-Singular ;
! Plural
:a   PL-NOM   ; ! kanat
:o	     PL-PAR_A  ; ! kanoja
! The next one is causing problems
! I can't get the i:j
:o  PL-PAR_A-LCns   ; ! kannoja
!+Pl+Par:o%>%^LCnsjAÄ1 K ; ! 2012-10-18
:o%>%^LCns   PL-ILL_blank   ; ! kannoi
:o Oblique-Plural ;

!! Three-syllable words

! This is incase there are 3-syllable issues
LEXICON N_MONIKKO
 N_MATO ;

LEXICON N_TYTÄR
! tytär:tyttä
@U.POS.N@+N:@U.POS.N@%>%^WCnsr    SG-NOM        ;
@U.POS.N@+N:@U.POS.N@%>%^WCnsr    SG-PAR_tA        ;
@U.POS.N@+N:@U.POS.N@r N-tytär_01 ;

LEXICON N-tytär_01
:e     SG-ILL_V		;
:e Oblique-Singular ;
:e     PL-NOM		;
     PL-GEN		;
     PL-PAR_A		;
     PL-ILL_V		;
 Oblique-Plural ;

LEXICON N_PATSAS
! patsas:patsa
+N:%>V2 DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-patsas_01 ;

LEXICON N-patsas_01
+Sg+Nom:%>%^WCnss K                ;
       :%>%^WCnss SG-PAR_tA        ;
       :%>V2      SG-ILL_sse       ;
       :h%>V2     SG-ILL_sse       ;
       :%>V2      Oblique-Singular ;
       :h%>V2     Oblique-Singular ;
! Plural
       :%>V2      PL-NOM           ;
       :h%>V2     PL-NOM           ;
       :%>       PL-GEN           ;
       :%>V2      PL-PAR_tA        ;
       :h%>%^LCns PL-PAR_A         ;
       :%>V2      PL-ILL_sse       ;
       :h         PL-ILL_sse       ;
       :%>V2      Oblique-Plural   ;
       :hi        Oblique-Plural ;


LEXICON N_LÄHE
! lähe:lähte
+N: DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-lähe_01 ;
@U.POS.N@+N:@U.POS.N@%>%^WCns     SG-NOM        ;
@U.POS.N@+Err/Orth+N:@U.POS.N@%>%^WCnsh     SG-NOM               ;

LEXICON N-lähe_01
:%>%^WCnst       SG-PAR_tA 	;
+Sg+Par:%>%^WCnsttAÄ1 K ;
:%>%^LCnsV2      SG-ILL_sse		;
:h%>V2      SG-ILL_sse		;
:%>%^LCnsV2 Oblique-Singular ;
:h%>V2 Oblique-Singular ;
:%>%^LCnsV2      PL-NOM		;
:h%>V2      PL-NOM		;
:%>%^LCns Oblique-Plural_sse ;
:h Oblique-Plural_sse ;
! Check 2012-10-12
:      PL-PAR_tA		;
:h%>%^LCns      PL-PAR_A		;
:h%>%^LCnsi      PL-ESS_Vn		;





LEXICON N_SLOVARI
! from mato (II-e)
+N: DER-tOin ; ! slovaritoin
@U.POS.N@+N:@U.POS.N@ N-slovari_01 ;

LEXICON N-slovari_01
 SG-NOM   ; ! slovari
+Sg+Par:%>AÄ1	#   ; ! mattooa
+Sg+Ill:%>%^LVws	#   ; ! mattoo
 Oblique-Singular ;
! Plural
: lOi-Pl ;
 PL-NOM   ; ! maot
:%^NoGrad Oblique-Plural ;
 PL-PAR_tA ;
 PL-ILL_hV ;


LEXICON N_NUMERO
! numero:numero
+N: DER-tOin ; ! numerotoin
@U.POS.N@+N:@U.POS.N@ N-numero_01 ;

LEXICON N-numero_01
 SG-NOM   ; ! numero
 SG-PAR_A   ; ! mattooa
 SG-ILL_V   ; ! mattoo
 Oblique-Singular ;
! Plural
: lOi-Pl ;
 PL-NOM   ; ! maot
:%^NoGrad Oblique-Plural ;
 PL-PAR_tA ;
 PL-ILL_hV ;


LEXICON N_VOKALA
! the stem-final "a" is added here
! vokala (IV-b)
+N:a DER-tOin ; ! vokalatoin
@U.POS.N@+N:@U.POS.N@ N-vokala_01 ;

LEXICON N-vokala_01
:a     SG-NOM   ; ! vokala
:a SG-PAR_A   ; ! jalkaa
:a SG-ILL_V   ; ! jalkaa
:a  Oblique-Singular ;
! Plural
:a PL-NOM   ; ! vokalat
:o PL-PAR_A   ; ! vokaloja
:o PL-PAR_tA   ; ! praaviloita
:o PL-ILL_hV   ; ! jalkoihe
:o  Oblique-Plural ;


LEXICON N_HARAKKA
! harakka:harakk
! This should operate using principles of ommeena
+N:AÄ1 DER-tOin ; !harakatoin
@U.POS.N@+N:@U.POS.N@ N-harakka_01 ;

LEXICON N-harakka_01
+Sg+Nom:AÄ1     #   ; ! harakka
+Sg+Gen:AÄ1%^StretchSyll2%>%^WCnsn	#   ; ! harraakan
+Sg+Par:AÄ1AÄ1	#   ; ! mattooa
+Sg+Ill:AÄ1%>%^LCnsV1	#   ; ! mattoo
+Sg+Ine:AÄ1%>%^WCnsAÄ1s    #   ; ! maoos
+Sg+Ine:AÄ1%>%^WCnss    #   ; ! maoos
+Sg+Ela:AÄ1%>%^WCnsst    #   ; ! maost
+Sg+All:AÄ1%>%^WCnslle    #   ; ! maolle
+Sg+Ade:AÄ1%>%^WCnsl    #   ; ! maool
+Sg+Ade:AÄ1%>%^WCnsAÄ1l    #   ; ! maool
+Sg+Abl:AÄ1%>%^WCnslt    #   ; ! maolt
+Sg+Tra:AÄ1%>%^WCnsks    #   ; ! maoks
+Sg+Ess:AÄ1%>%^LCns%^LVwsnnAÄ1    #   ; ! mattoon
+Sg+Exe:AÄ1%>%^LCns%^LVwsnt    #   ; ! mattoon
+Sg+Com:AÄ1%>%^WCnsnkAÄ1    #   ; ! maanka
! Plural
:OÖ1  Oblique-Plural_hV ;
:OÖ1 PL-PAR_A ;
+Pl+Nom:AÄ1%^StretchSyll2%^WCnst     #   ; ! maot

LEXICON N_OMMEENA
! ommeena:omena
+N:AÄ1 DER-tOin ; !omenatoin
@U.POS.N@+N:@U.POS.N@ N-ommeena_01 ;

LEXICON N-ommeena_01
+Sg+Nom:AÄ1%^StretchSyll2     #   ; ! mato
+Sg+Gen:AÄ1%^StretchSyll2n	#   ; ! maon
+Sg+Par:AÄ1AÄ1	#   ; ! mattooa
+Sg+Ill:AÄ1%>%^LCnsV1	#   ; ! mattoo
+Sg+Ine:AÄ1%>%^WCnsAÄ1s    #   ; ! maoos
+Sg+Ine:AÄ1%>%^WCnss    #   ; ! maoos
+Sg+Ela:AÄ1%>%^WCnsst    #   ; ! maost
+Sg+All:AÄ1%>%^WCnslle    #   ; ! maolle
+Sg+Ade:AÄ1%>%^WCnsl    #   ; ! maool
+Sg+Ade:AÄ1%>%^WCnsAÄ1l    #   ; ! maool
+Sg+Abl:AÄ1%>%^WCnslt    #   ; ! maolt
+Sg+Tra:AÄ1%>%^WCnsks    #   ; ! maoks
+Sg+Ess:AÄ1%>%^LCns%^LVwsnnAÄ1    #   ; ! mattoon
+Sg+Exe:AÄ1%>%^LCns%^LVwsnt    #   ; ! mattoon
+Sg+Com:AÄ1%>%^WCnsnkAÄ1    #   ; ! maanka
! Plural
:OÖ1 Oblique-Plural_hV ;
:OÖ1 PL-PAR_A ;
:OÖ1 PL-PAR_tA ;
:AÄ1%^StretchSyll2 PL-NOM   ; ! maot

LEXICON N_PUTTEELI
! putteeli:puteli
! 
+N: DER-tOin ; !putelitoin
@U.POS.N@+N:@U.POS.N@ N-putteeli_01 ;

LEXICON N-putteeli_01
+Sg+Nom:%^StretchSyll2     #   ; ! mato
+Sg+Gen:%^StretchSyll2n	#   ; ! maon
+Sg+Par:AÄ1	#   ; ! mattooa
+Sg+Ill:i	#   ; ! mattoo
+Sg+Ine:%^StretchSyll2is    #   ; ! maoos
+Sg+Ine:%^StretchSyll2s    #   ; ! maoos
+Sg+Ela:%^StretchSyll2st    #   ; ! maost
+Sg+All:%^StretchSyll2lle    #   ; ! maolle
+Sg+Ade:%^StretchSyll2l    #   ; ! maool
+Sg+Abl:%^StretchSyll2lt    #   ; ! maolt
+Sg+Tra:%^StretchSyll2ks    #   ; ! maoks
+Sg+Ess:%>%^LCns%^LVwsnnAÄ1    #   ; ! mattoon
+Sg+Exe:%>%^LCns%^LVwsnt    #   ; ! mattoon
+Sg+Com:%>%^WCnsnkAÄ1    #   ; ! maanka
! Plural
:%^StretchSyll2  lOi-Pl ; ! putteeliloin
+Pl+Nom:%^StretchSyll2t     #   ; ! maot


LEXICON N_LEIKKUU
! leikkuu, kiukkaa, lämmää (VI)
+N: DER-tOin ; ! leikkuutoin
@U.POS.N@+N:@U.POS.N@ N-leikkuu_01 ;

LEXICON N-leikkuu_01
+Sg+Nom:     #   ; ! mato
+Sg+Par:%>tAÄ1	#   ; ! ?lämmäätä vs 
+Sg+Ill:%>sse	#   ; ! mattoo
 Oblique-Singular ;
! Plural
 lOi-Pl ;
+Pl+Nom:%>t     #   ; ! maot

LEXICON N_ASSIA
! assia:assi (VII)
+N:AÄ1 DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-assia_01 ;

LEXICON N-assia_01
:AÄ1 N-assia_01-Sg ;
:OÖ1  Oblique-Plural ;
:OÖ1 PL-PAR_A ;
:OÖ1 PL-ILL_hV ;

LEXICON N-assia_01-Sg
 SG-NOM	    ; ! assia
 SG-PAR_A   ; ! assiaa
 SG-ILL_V   ; ! assiaa
 Oblique-Singular ;
! Plural
 PL-NOM  ; ! assiat

LEXICON N_PESÄ
! VCV
! pesä:pesä
+N: DER-tOin ; !pesätöin
@U.POS.N@+N:@U.POS.N@ N-pesä_01 ;

LEXICON N-pesä_01
 SG-NOM   ; ! pesä
 SG-PAR_A-LCns   ; ! pessää
 SG-ILL_V   ; ! pessää
 Oblique-Singular ;
! Plural
 PL-NOM   ; ! pesät
 PL-PAR_A-LCns   ; ! pessiä
 PL-ILL_V   ; ! pessii
 Oblique-Plural ;

LEXICON N_TÄHKÄ
! VCCV
! tähkä (VIII)
+N: DER-tOin ; !tähätöin
@U.POS.N@+N:@U.POS.N@ N-tähkä_01 ;

LEXICON N-tähkä_01
 SG-NOM   ; ! mato
 SG-PAR_A-LCns   ; ! mattooa
 SG-ILL_V   ; ! mattoo
 Oblique-Singular ;
! Plural
 PL-NOM   ; ! tähät
 PL-PAR_A   ; ! tähkiä
 PL-ILL_V   ; ! tähkii
 Oblique-Plural ;

LEXICON N_TAEHTI
! tähti (IX)
+N:e DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ NMN_TAEHTI_01 ;

LEXICON NMN_TAEHTI_01
:i     SG-NOM   ; ! tähti
:e SG-PAR_A   ; ! tähteä
:e SG-ILL_V   ; ! tähtee
:e  Oblique-Singular ;
! Plural
:i  lOi-Pl ;
+Pl+Nom:e%>%^WCnst     #   ; ! maot
+Pl+Acc:e%>%^WCnst     #   ; ! maot
 PL-PAR_A ;
 PL-ILL_V ;
 Oblique-Plural ;

LEXICON N_VARSI
 N_KÄSI ;
@U.POS.N@+N:@U.POS.N@s     SG-NOM   ; ! vars

LEXICON N_VOOSI
! voosi:voo
@U.POS.N@+N+Sg+Ess:@U.POS.N@nnAÄ1 K ; ! voonna 
@U.POS.N@+N:@U.POS.N@ N-käsi_01 ;

LEXICON N_KÄSI
! käsi (XI)
+N:te DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-käsi_01 ; 

LEXICON N-käsi_01
:si     SG-NOM   ; ! orsi
:t SG-PAR_tA  ; ! ortta
:te SG-ILL_V  ; ! ortee
:te%>%^LCns SG-ESS_nnA ;
:te%>%^LCns SG-EXE ;
:te  Oblique-Singular_less-EssExe ;
! Plural
:te PL-NOM   ; ! maot
:s PL-PAR_A-LCns  ;
:s PL-ILL_V   ; ! kässii
:si%>%^LCns PL-ESS_nnA ;
:si%>%^LCns PL-ESS_n ;
:s%>%^LCns PL-EXE ;
:s Oblique-Plural_less-EssExe ;

LEXICON N_MEES
! mees:mee (??)
+N:he DER-tOin ; !meehetöin
@U.POS.N@+N:@U.POS.N@ N-mees_01 ; 

LEXICON N-mees_01
:s     SG-NOM   ; ! mees
:s SG-PAR_tA  ; ! meestä
:he SG-ILL_V  ; ! meehe ??
:he  Oblique-Singular ;
! Plural
:he PL-NOM   ; ! meehet
:s PL-PAR_A-LCns  ;
:s PL-ILL_V   ; ! kässii
:s Oblique-Plural ;

LEXICON N_VARIS
! varis (XII)
+N:kse DER-tOin ; !variksetoin
@U.POS.N@+N:@U.POS.N@ N-varis_01 ;

LEXICON N-varis_01
:s   SG-NOM  ;
+Sg+Par:s%>t	#   ; ! varist
:s SG-PAR_tA  ;
:kse SG-ILL_V ;
!+Sg+Ill:kse%>%^LCnsV1	#   ; ! mattoo
:kse Oblique-Singular ;
! Plural
:kse PL-NOM   ; ! varikset
+Pl+Gen:ks%>i%>n	#   ; ! variksin
+Pl+Par:ks%>i%>AÄ1	#   ; ! variksia
:ks PL-ILL_V   ; ! variksii
:ks  Oblique-Plural ;

LEXICON N_NAIN
! nain:nai
! 1     2
+N:se DER-tOin ; !kärpäsetöin
@U.POS.N@+N:@U.POS.N@ N-nain_01 ;

LEXICON N-nain_01
:n            SG-NOM  ; ! nain
:s     	      SG-PAR_tA ; ! naista
+Sg+Par:s%>t  K   ; ! naist
:se    	      SG-ILL_V   ; !
:se Oblique-Singular ;
! Plural
:se     PL-NOM   ; ! naiset
:s      PL-PAR_A   ; ! 
:s      PL-ILL_V   ; ! 
:s Oblique-Plural ;


LEXICON N_KÄRPÄIN
! kärpäin:kärpäi (XIII)
!   2       3
+N:se DER-tOin ; !kärpäsetöin
@U.POS.N@+N:@U.POS.N@ N-kärpäin_01 ;

LEXICON N-kärpäin_01
:n            SG-NOM  ;
:s     	      SG-PAR_tA ; ! kärpäistä
+Sg+Par:s%>t  K   ; ! kärpäist
:se    	      SG-ILL_V   ; !
:se Oblique-Singular ;
! Plural
:se     PL-NOM   ; ! kärpäiset
:s      PL-PAR_A   ; ! variksia
:s      PL-ILL_V   ; ! kärpäisii
:s Oblique-Plural ;

LEXICON N_HOOMIIN
! hoomiin:hoomi (XIII)
!   2       3
+N:se DER-tOin ; !kärpäsetöin
@U.POS.N@+N:@U.POS.N@ N-hoomiin_01 ;

LEXICON N-hoomiin_01
:in            SG-NOM  ;
:s     	      SG-PAR_tA ; ! hoomista
+Sg+Par:s%>t  K   ; ! hoomist
:se    	      SG-ILL_V   ; !
:se Oblique-Singular ;
! Plural
:se     PL-NOM   ; ! hoomiset
:s      PL-PAR_A   ; ! hoomisia
:s      PL-ILL_V   ; ! hoomisii
:s Oblique-Plural ;

LEXICON N_MEHILÄIN
! (XIII:kärpäin)<
! mehiläin:mehiläi
!    3        4
+N:se DER-tOin ; !mehiläisetöin
@U.POS.N@+N:@U.POS.N@ N-mehiläin_01 ;

LEXICON N-mehiläin_01
:n            SG-NOM  ;
:s     	      SG-PAR_tA ; ! mehiläistä
+Sg+Par:s%>t  K   ; ! mehiläist
:se    	      SG-ILL_V   ; !
:se  Oblique-Singular ;
! Plural
:se     PL-NOM   ; ! mehiläiset
:s      PL-PAR_A-LCns   ; ! mehiläissiä
:s      PL-PAR_A   ; ! mehiläisiä
:s      PL-ILL_V   ; ! mehiläisii
:s    Oblique-Plural ;

LEXICON N_LAPSI
! lapsi:la
+N:pse DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-lapsi_01 ;

LEXICON N-lapsi_01
:ps SG-NOM ; ! laps
:psi SG-NOM ; ! lapsi
:s 		    SG-PAR_tA ; ! veistä
:pse		    SG-ILL_V   ; !
:pse Oblique-Singular ;
! Plural
:pse     PL-NOM   ; ! veitset
:ps PL-PAR_A   ; ! variksia
:ps PL-ILL_V  ; ! kärpäisii
:ps    Oblique-Plural ;

LEXICON N_VEITSI
! veitsi:vei
+N:tse DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-veitsi_01 ;

LEXICON N-veitsi_01
:tsi SG-NOM ; ! veitsi
:s 		    SG-PAR_tA ; ! veistä
:tse		    SG-ILL_V   ; !
:tse Oblique-Singular ;
! Plural
:tse     PL-NOM   ; ! veitset
:ts PL-PAR_A   ; ! variksia
:ts PL-ILL_V  ; ! kärpäisii
:ts    Oblique-Plural ;


LEXICON N_INMIHIIN
! inmihiin (XIII-b)
+N:se DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-inmihiin_01 ;

LEXICON N-inmihiin_01
:in            SG-NOM  ; !inmihiin
:s     	      SG-PAR_tA ; ! kärpäistä
+Sg+Par:s%>t  K   ; ! kärpäist
:se    	      SG-ILL_V   ; !
:se Oblique-Singular ;
! Plural
:se     PL-NOM   ; ! kärpäiset
:s      PL-PAR_A-LCns   ; ! variksia
:s      PL-ILL_V   ; ! kärpäisii
:s    Oblique-Plural ;


LEXICON N_MERI
 N_KEELI ;

LEXICON N_KEELI
! keeli (IX-b)
+N:e DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-keeli_01 ;

LEXICON N-keeli_01
:i     SG-NOM   ; ! keeli
+Err/Orth+Sg+Par:t K ;
 SG-PAR_tA  ; ! keeltä
:e SG-ILL_V   ; ! keelee
:e Oblique-Singular ;
! Plural
:e PL-NOM   ; ! keelet
 PL-PAR_A   ; ! matoloja
 PL-ILL_V   ; ! keelii
 Oblique-Plural ;


LEXICON N_SAMMAL
+N:e DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-sammal_01 ;

LEXICON N-sammal_01
     SG-NOM   ; ! sammal
 SG-PAR_tA  ; ! keeltä
:e%>%^LCns SG-ILL_V   ; ! mattoo
:e Oblique-Singular ;
! Plural
:e PL-NOM   ; ! keelet
 PL-PAR_A   ; ! matoloja
 PL-ILL_V   ; ! keelii
 Oblique-Plural ;


! (XIV)
LEXICON N-XIV
#;

! (XV)
LEXICON N-XV
#;

! (XVI)
LEXICON N-XVI
#;

! (XVII)
LEXICON N-XVII
#;

! (XVIII)
LEXICON N-XVIII
#;

! (XIX)
LEXICON N-XIX
#;

LEXICON N_OIKEHUS
! (XX) oikehus
+N:e DER-tOin ; !
@U.POS.N@+N:@U.POS.N@ N-oikehus_01 ;

LEXICON N-oikehus_01
+Sg+Nom:s           K  ;
+Sg+Par:t%>tAÄ1	#   ; ! kärpäistä
:e SG-ILL_sse   ; ! oikehuesse
:e Oblique-Singular ;

! Plural
:e PL-NOM  ; ! oikehuet
:ks Oblique-Plural_V ;
:ks PL-PAR_A ;


LEXICON N_VENÄT
! kevät (XX-a)
! venät:venä
@U.POS.N@+N:@U.POS.N@ N-venät_01 ;

LEXICON N-venät_01
 N-venät_01-Sg ;
:%^SyllBr Oblique-Plural ;
:%^SyllBr PL-PAR_tA ;

LEXICON N-venät_01-Sg
:%^SyllBre%^StretchSyll2 SG-GEN ;
:%^SyllBre%^StretchSyll2 PL-NOM ;
:t SG-NOM ;
:t SG-PAR_tA ;
:e SG-ILL_sse ;
:e SG-INE ;
:ee SG-INE ;
:e SG-ELA ;
:e SG-ALL ;
:e SG-ADE ;
:ee SG-ADE ;
:e SG-ABL ;
:e SG-TRA ;
:%^SyllBre%^StretchSyll2 SG-TRA ;
:e SG-ESS_nnA ;
:e SG-EXE ;

LEXICON N_PÄIVYT
! päivyt:päivy
@U.POS.N@+N:@U.POS.N@ N-päivyt_01 ;

LEXICON N-päivyt_01
 N-venät_01-Sg ;
:e Oblique-Plural_sse ;
:e PL-PAR_tA ;


! (XXI)
LEXICON N-XXI
#;

!
!! ## DERIVATION 
!
!! One derivational process only, DER-t0in.

LEXICON DER-tOin
@C.POS@+Der/tOin:@C.POS@%>%^WCnst A_KALATOIN ;

!
!! ## DECLENSION 
!

LEXICON Cases
!+N+Sg+Nom:     #   ;
!+N+Pl+Nom:i     #   ;
+N+Sg+Ine:%>%^WCnsz%^ShVws    #   ; ! opokka : opokaaz
+N+Sg+Ela:%>%^WCnsst    #   ; ! jalka : jalast
+N+Sg+All:%>%^WCnsle    #   ; ! katto : katolle
+N+Sg+Ade:%>%^WCnsl%^ShVws    #   ; ! katto : katool
+N+Sg+Abl:%>%^WCnslt    #   ; ! katto : katolt
+N+Sg+Tra:%>%^WCnsks    #   ; ! musta : musaks
+N+Sg+Ess:%>n%^ShVws    #   ; ! sotilaz : sotilahhaan, 


! Fed by +N+Pl, +N+Prop+Pl, +A+Pl
LEXICON lOi-Pl
:%>lOÖ1 Oblique-Plural_hV ;
:lOÖ1 PL-PAR_A ;
:lOÖ1 PL-PAR_tA ;

LEXICON Oblique-Plural_blank
 PL-ILL_blank ;
 Oblique-Plural ;

LEXICON Oblique-Plural_sse
 PL-ILL_sse ;
 Oblique-Plural ;

LEXICON Oblique-Plural_V
 PL-ILL_V ;
 Oblique-Plural ;

LEXICON Oblique-Plural_hV
 PL-ILL_hV ;
 Oblique-Plural ;

LEXICON Oblique-Plural
Oblique-Plural_less-EssExe ;
      PL-ESS_Vn		;
      PL-ESS_nnA	;
      PL-ESS_n	;
      PL-EXE		;


LEXICON Oblique-Plural_less-EssExe
      PL-GEN		;
      PL-INE		;
      PL-ELA		;
      PL-ALL		;
      PL-ADE		;
      PL-ABL		;
      PL-TRA		;
      PL-COM		;
      PL-INS	;
      PL-PRL		;
      PL-ABE		;

LEXICON Oblique-Singular
 Oblique-Singular_less-EssExe ;
      SG-ESS_Vn		;
      SG-ESS_nnA	;
      SG-EXE		;

LEXICON Oblique-Singular_less-EssExe
      SG-GEN		;
      SG-INE		;
      SG-ELA		;
      SG-ALL		;
      SG-ADE		;
      SG-ABL		;
      SG-TRA		;
      SG-COM		;
      SG-ABE		;

!
!! ## PLURAL TAGS 
!
!! Plural tags separated from singular ones.


LEXICON PL-ABE
@U.DECL-NX.PL@@U.DECL-CX.ABE@+Pl+Abe0000:@U.DECL-NX.PL@@U.DECL-CX.ABE@%>i%>%^WCnstAÄ1    K_Num  ;

LEXICON PL-ABL
@U.DECL-NX.PL@@U.DECL-CX.ABL@+Pl+Abl0000:@U.DECL-NX.PL@@U.DECL-CX.ABL@%>i%>%^WCnslt    K_Num  ;
!2012-11-03!@R.POS.N@ PL-ABL_Px ;

LEXICON PL-ABL_Px
@U.DECL-NX.PL@@U.DECL-CX.ABL@+Pl+Abl+PxSg300000:@U.DECL-NX.PL@@U.DECL-CX.ABL@%>i%>%^WCnsltAÄ1AÄ1  K_Num  ;

LEXICON PL-ADE
@U.DECL-NX.PL@@U.DECL-CX.ADE@+Pl+Ade0000:@U.DECL-NX.PL@@U.DECL-CX.ADE@%>i%>%^WCnsV3l    K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.ADE@+Pl+Ade000:@U.DECL-NX.PL@@U.DECL-CX.ADE@%>i%>%^WCnsl    K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.ADE@+Pl+Ade+PxSg300000:@U.DECL-NX.PL@@U.DECL-CX.ADE@%>i%>%^WCnsllAÄ1AÄ1  K_Num  ;

LEXICON PL-ALL
@U.DECL-NX.PL@@U.DECL-CX.ALL@+Pl+All00000:@U.DECL-NX.PL@@U.DECL-CX.ALL@%>i%>%^WCnslle    K_Num  ; 
@U.DECL-NX.PL@@U.DECL-CX.ALL@+Pl+All+PxSg300000:@U.DECL-NX.PL@@U.DECL-CX.ALL@%>i%>%^WCnsllee  K_Num  ; 

LEXICON PL-COM
!+Dial/Lauk
@U.DECL-NX.PL@@U.DECL-CX.COM@+Pl+Com00000:@U.DECL-NX.PL@@U.DECL-CX.COM@%>i%>%^WCnsnkAÄ1    K_Num ;

LEXICON PL-ELA
@U.DECL-NX.PL@@U.DECL-CX.ELA@+Pl+Ela0000:@U.DECL-NX.PL@@U.DECL-CX.ELA@%>i%>%^WCnsst    K_Num ;
@U.DECL-NX.PL@@U.DECL-CX.ELA@+Pl+Ela+PxSg300000:@U.DECL-NX.PL@@U.DECL-CX.ELA@%>i%>%^WCnsstAÄ1AÄ1    K_Num ;

LEXICON PL-ESS_Vn
!+Dial/Soik
@U.DECL-NX.PL@@U.DECL-CX.ESS@+Pl+Ess000:@U.DECL-NX.PL@@U.DECL-CX.ESS@%>%^LCns%>i%>V3n K_Num  ;

LEXICON PL-ESS_n
!+Dial/Soik
@U.DECL-NX.PL@@U.DECL-CX.ESS@+Pl+Ess0000:@U.DECL-NX.PL@@U.DECL-CX.ESS@%>i%>n    K_Num  ;

LEXICON PL-ESS_nnA
!+Dial/Lauk
@U.DECL-NX.PL@@U.DECL-CX.ESS@+Pl+Ess0000:@U.DECL-NX.PL@@U.DECL-CX.ESS@%>i%>nnAÄ1    K_Num  ;

LEXICON PL-EXE
@U.DECL-NX.PL@@U.DECL-CX.EXE@+Pl+Exe000:@U.DECL-NX.PL@@U.DECL-CX.EXE@%>i%>nt    K_Num  ;

LEXICON PL-GEN
@U.DECL-NX.PL@@U.DECL-CX.GEN@+Pl+Gen000:@U.DECL-NX.PL@@U.DECL-CX.GEN@%>%^LCns%>i%>V3n    K_Num  ;

LEXICON PL-ILL_blank
@U.DECL-NX.PL@@U.DECL-CX.ILL@+Pl+Ill000:@U.DECL-NX.PL@@U.DECL-CX.ILL@%>%^LCns%>i%>    K_Num   ;

LEXICON PL-ILL_sse
@U.DECL-NX.PL@@U.DECL-CX.ILL@+Pl+Ill0000:@U.DECL-NX.PL@@U.DECL-CX.ILL@%>i%>sse    K_Num   ;

LEXICON PL-ILL_V
@U.DECL-NX.PL@@U.DECL-CX.ILL@+Pl+Ill00:@U.DECL-NX.PL@@U.DECL-CX.ILL@%>%^LCns%>i%>V1    K_Num   ;

LEXICON PL-ILL_hV
@U.DECL-NX.PL@@U.DECL-CX.ILL@+Pl+Ill000:@U.DECL-NX.PL@@U.DECL-CX.ILL@%>i%>he    K_Num   ;

LEXICON PL-INE
@U.DECL-NX.PL@@U.DECL-CX.INE@+Pl+Ine0000:@U.DECL-NX.PL@@U.DECL-CX.INE@%>i%>%^WCnsV3s    K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.INE@+Pl+Ine000:@U.DECL-NX.PL@@U.DECL-CX.INE@%>i%>%^WCnss    K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.INE@+Pl+Ine+PxSg300000:@U.DECL-NX.PL@@U.DECL-CX.INE@%>i%>%^WCnsssAÄ1AÄ1    K_Num  ;

! instruktiva
! only with Px...
LEXICON PL-INS
@U.DECL-NX.PL@@U.DECL-CX.INS@+Pl+Instr+PxSg3000:@U.DECL-NX.PL@@U.DECL-CX.INS@%>i%>nee    #   ; ! lehminee

LEXICON PL-NOM
@U.DECL-NX.PL@@U.DECL-CX.NOM@+Pl+Nom0:@U.DECL-NX.PL@@U.DECL-CX.NOM@%>%^WCnst   K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.ACC@+Pl+Acc0:@U.DECL-NX.PL@@U.DECL-CX.ACC@%>%^WCnst   K_Num  ;

LEXICON PL-PAR_A
@U.DECL-NX.PL@@U.DECL-CX.PAR@+Pl+Par00:@U.DECL-NX.PL@@U.DECL-CX.PAR@%>i%>AÄ1      K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.PAR@+Pl+Par+PxSg300:@U.DECL-NX.PL@@U.DECL-CX.PAR@%>i%>AÄ1AÄ1      K_Num  ;

! kana:kano
LEXICON PL-PAR_A-LCns
@U.DECL-NX.PL@@U.DECL-CX.PAR@+Pl+Par00:@U.DECL-NX.PL@@U.DECL-CX.PAR@%>%^LCns%>i%>AÄ1      K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.PAR@+Pl+Par+PxSg30000:@U.DECL-NX.PL@@U.DECL-CX.PAR@%>%^LCns%>i%>AÄ1AÄ1      K_Num  ;


LEXICON PL-PAR_tA
@U.DECL-NX.PL@@U.DECL-CX.PAR@+Pl+Par000:@U.DECL-NX.PL@@U.DECL-CX.PAR@%>i%>tAÄ1      K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.PAR@+Pl+Par+PxSg3000:@U.DECL-NX.PL@@U.DECL-CX.PAR@%>i%>tAÄ1AÄ1      K_Num  ;

LEXICON PL-PRL
@U.DECL-NX.PL@@U.DECL-CX.PRL@+Pl+Prl00000:@U.DECL-NX.PL@@U.DECL-CX.PRL@%>i%>%^WCnstse    K   ; ! rannoitse

LEXICON PL-TRA
@U.DECL-NX.PL@@U.DECL-CX.TRA@+Pl+Tra0000:@U.DECL-NX.PL@@U.DECL-CX.TRA@%>i%>%^WCnsks   K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.TRA@+Pl+Tra+PxSg2000000:@U.DECL-NX.PL@@U.DECL-CX.TRA@%>i%>%^WCnsksees   K_Num  ;
@U.DECL-NX.PL@@U.DECL-CX.TRA@+Pl+Tra+PxSg300000:@U.DECL-NX.PL@@U.DECL-CX.TRA@%>i%>%^WCnsksee   K_Num  ;

LEXICON SG-ABE
@U.DECL-NX.SG@@U.DECL-CX.ABE@+Sg+Abe00:@U.DECL-NX.SG@@U.DECL-CX.ABE@%>%^WCnstAÄ1    K_Num  ;

LEXICON SG-ABL
@U.DECL-NX.SG@@U.DECL-CX.ABL@+Sg+Abl00:@U.DECL-NX.SG@@U.DECL-CX.ABL@%>%^WCnslt    K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.ABL@+Sg+Abl+PxSg3000:@U.DECL-NX.SG@@U.DECL-CX.ABL@%>%^WCnsltAÄ1AÄ1  K_Num  ;

LEXICON SG-ADE
@U.DECL-NX.SG@@U.DECL-CX.ADE@+Sg+Ade00:@U.DECL-NX.SG@@U.DECL-CX.ADE@%>%^WCnsV3l    K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.ADE@+Sg+Ade0:@U.DECL-NX.SG@@U.DECL-CX.ADE@%>%^WCnsl    K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.ADE@+Sg+Ade+PxSg3000:@U.DECL-NX.SG@@U.DECL-CX.ADE@%>%^WCnsllAÄ1AÄ1  K_Num  ;

LEXICON SG-ALL
@U.DECL-NX.SG@@U.DECL-CX.ALL@+Sg+All000:@U.DECL-NX.SG@@U.DECL-CX.ALL@%>%^WCnslle    K_Num  ; 
@U.DECL-NX.SG@@U.DECL-CX.ALL@+Sg+All+PxSg3000:@U.DECL-NX.SG@@U.DECL-CX.ALL@%>%^WCnsllee  K_Num  ; 

LEXICON SG-COM
@U.DECL-NX.SG@@U.DECL-CX.COM@+Dial/Lauk+Sg+Com00:@U.DECL-NX.SG@@U.DECL-CX.COM@%>%^WCnsnkAÄ1    K_Num ;
@U.DECL-NX.SG@@U.DECL-CX.COM@+Dial/Lauk+Sg+Com+PxSg30:@U.DECL-NX.SG@@U.DECL-CX.COM@%>%^LVwsnkAÄ1    K_Num ;

LEXICON SG-ELA
@U.DECL-NX.SG@@U.DECL-CX.ELA@+Sg+Ela00:@U.DECL-NX.SG@@U.DECL-CX.ELA@%>%^WCnsst    K_Num ;
@U.DECL-NX.SG@@U.DECL-CX.ELA@+Sg+Ela+PxSg3000:@U.DECL-NX.SG@@U.DECL-CX.ELA@%>%^WCnsstAÄ1AÄ1    K_Num ;

LEXICON SG-ESS_Vn
@U.DECL-NX.SG@@U.DECL-CX.ESS@+Sg+Ess00:@U.DECL-NX.SG@@U.DECL-CX.ESS@%>%^LCnsV3n K_Num  ;

LEXICON SG-ESS_nnA
@U.DECL-NX.SG@@U.DECL-CX.ESS@+Sg+Ess0:@U.DECL-NX.SG@@U.DECL-CX.ESS@nnAÄ1    K_Num  ;

LEXICON SG-EXE
@U.DECL-NX.SG@@U.DECL-CX.EXE@+Sg+Exe:@U.DECL-NX.SG@@U.DECL-CX.EXE@nt    K_Num  ;

LEXICON SG-GEN
@U.DECL-NX.SG@@U.DECL-CX.GEN@+Sg+Gen0:@U.DECL-NX.SG@@U.DECL-CX.GEN@%>%^WCnsn      K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.ACC@+Sg+Acc0:@U.DECL-NX.SG@@U.DECL-CX.ACC@%>%^WCnsn      K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.GEN@+Sg+Gen+PxSg3:@U.DECL-NX.SG@@U.DECL-CX.GEN@%>%^LVwsn      K_Num  ;

LEXICON SG-ILL_sse
@U.DECL-NX.SG@@U.DECL-CX.ILL@+Sg+Ill0:@U.DECL-NX.SG@@U.DECL-CX.ILL@sse    K_Num   ;

LEXICON SG-ILL_V
@U.DECL-NX.SG@@U.DECL-CX.ILL@+Sg+Ill:@U.DECL-NX.SG@@U.DECL-CX.ILL@%>%^LCns%>V1    K_Num   ;

LEXICON SG-ILL_hV
@U.DECL-NX.SG@@U.DECL-CX.ILL@+Sg+Ill0:@U.DECL-NX.SG@@U.DECL-CX.ILL@%>hV1    K_Num   ;
@U.DECL-NX.SG@@U.DECL-CX.ILL@+Sg+Ill+PxSg300:@U.DECL-NX.SG@@U.DECL-CX.ILL@%>hV1%>V3    K_Num   ;

LEXICON SG-INE
@U.DECL-NX.SG@@U.DECL-CX.INE@+Sg+Ine00:@U.DECL-NX.SG@@U.DECL-CX.INE@%>%^WCnsV3s    K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.INE@+Sg+Ine0:@U.DECL-NX.SG@@U.DECL-CX.INE@%>%^WCnss    K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.INE@+Sg+Ine+PxSg3000:@U.DECL-NX.SG@@U.DECL-CX.INE@%>%^WCnsssAÄ1AÄ1    K_Num  ;

LEXICON SG-NOM
@U.DECL-NX.SG@@U.DECL-CX.NOM@+Sg+Nom:@U.DECL-NX.SG@@U.DECL-CX.NOM@    K_Num ;

LEXICON SG-PAR_A
@U.DECL-NX.SG@@U.DECL-CX.PAR@+Sg+Par:@U.DECL-NX.SG@@U.DECL-CX.PAR@AÄ1      K_Num  ;

LEXICON SG-PAR_A-LCns
@U.DECL-NX.SG@@U.DECL-CX.PAR@+Sg+Par0:@U.DECL-NX.SG@@U.DECL-CX.PAR@%>%^LCnsAÄ1      K_Num  ;

LEXICON SG-PAR_tA
@U.DECL-NX.SG@@U.DECL-CX.PAR@+Sg+Par:@U.DECL-NX.SG@@U.DECL-CX.PAR@tAÄ1      K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.PAR@+Sg+Par+PxSg3:@U.DECL-NX.SG@@U.DECL-CX.PAR@tAÄ1AÄ1      K_Num  ;

LEXICON SG-TRA
@U.DECL-NX.SG@@U.DECL-CX.TRA@+Sg+Tra00:@U.DECL-NX.SG@@U.DECL-CX.TRA@%>%^WCnsks   K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.TRA@+Sg+Tra+PxSg20000:@U.DECL-NX.SG@@U.DECL-CX.TRA@%>%^WCnsksees   K_Num  ;
@U.DECL-NX.SG@@U.DECL-CX.TRA@+Sg+Tra+PxSg3000:@U.DECL-NX.SG@@U.DECL-CX.TRA@%>%^WCnsksee   K_Num  ;

! vim: set ft=xfst-lexc:



