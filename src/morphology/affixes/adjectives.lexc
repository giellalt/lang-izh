!! Adjective inflection
!! --------------------
!! The UNDEFINED language adjectives compare.

LEXICON Comparisons
+A+pos:0        Cases   ;
+A+cmp:er       Cases   ;
+A+sup:est      Cases   ;

LEXICON A-harmaa
+A: A-harmaa_01 ;

LEXICON A-harmaa_01
 SG-NOM   ; ! harmaa
 SG-PAR_tA   ; ! harmaata
 SG-ILL_sse  ; ! harmaasse
 Oblique-Singular ;

 PL-NOM   ; ! harmaat
+Pl+Gen:%>i%>jen	#   ; ! harmaijen
 PL-PAR_tA  ; ! harmaita
 PL-ILL_sse   ; ! harmaisse
 Oblique-Plural ;


! (II) N-mato
LEXICON A-avvaaro
+A+Der/st:%>%^WCnsst AdvManner ; !
+A: # ;

!
LEXICON A-lihhaava
+A+Der/st:AÄ1%>%^StretchSyll2st AdvManner ; !
!+A+Comp:AÄ1%>%^StretchSyll2mp MP ; !
+A+Comp:AÄ1mp MP ; !
+A: N-ommeena_01 ;

! (XI) Num-viis
! uusi:uu
LEXICON A-uusi
+A+Der/st:te%>%^WCnsst AdvManner ; !
+A+Comp:te%>%^WCnsmp MP ; !
+A:si SG-NOM ;
+A: Num-viis_01 ;

!check this 2012-10-29
! vesselä:vesselä
LEXICON A-vesselä
+A+Der/st:%>%^WCnsst AdvManner ; !
+A+Comp:%>%^WCnsmp MP ; !
+A: N-osa_01 ;

! valmis:valmi
LEXICON A-valmis
+A: N-patsas_01 ;

! kalakas:kalakka
!LEXICON A-kalakas
LEXICON A-kitsas
+A: N-patsas_01 ;
+A+Der/st:ast AdvManner ; !
+A+Comp:amp MP ; !
+A+Superl:imp MP ; !
!+A: A-kalakas_01 ;

LEXICON A-hoono
+A+Comp:mp MP ; !
+A+Superl:imp MP-Oblique ; !
+A: N-mato_01 ;

LEXICON A-suur
+A+Der/st:e%>%^WCnsst AdvManner ; !
+A+Comp:e%>%^WCnsmp MP ; !
+A+Sg+Nom: K ;
+A+Sg+Nom:i K ;
+A: N-sammal_01 ;

! peeni:peen
LEXICON A-peeni
+A+Der/st:e%>%^WCnsst AdvManner ; !
+A+Comp:e%>%^WCnsmp MP ; !
+A: N-keeli_01 ;

LEXICON A-hyvä
+A: N-osa_01 ;
+A+Der/st:%>%^WCnsst AdvManner ; !

! oma (??)
LEXICON A-oma
+A: N-osa_01 ;
+A+Der/st:%>%^WCnsst AdvManner ; !

! kova:kova (??)
LEXICON A-kova
+A+Der/st:%>%^WCnsst AdvManner ; !
+A+Comp:%>%^WCns%^E1mp MP ; !
+A: N-osa_01 ;

! tulova:tulovAÄ1
LEXICON A-tulova
+Dial/Lauk+A+Der/st:%>%^WCnsst AdvManner ; !
+A+Comp:%>%^WCnsmp MP ; !
+A: N-osa_01 ;


! 3 syllables
LEXICON A-kalakas_01
+Sg+Nom:%>%^WCnss      K                ;
:%>%^WCnss	       SG-PAR_tA 	;
:%>V2      SG-ILL_sse		;
:h%>V2      SG-ILL_sse		;
:%>V2      Oblique-Singular ;
:h%>V2V2   Oblique-Singular ;
! Plural
:%>V2      PL-NOM		;
:h%>V2      PL-NOM		;
:      PL-PAR_tA		;
:hh      PL-PAR_A		;
:      PL-ILL_sse		;
:h      PL-ILL_sse		;
 Oblique-Plural ;
:hi     Oblique-Plural ;


! kerkiä:kerki (??)
LEXICON A-kerkiä
+A+Der/st:AÄ1%>%^WCnsst AdvManner ; !
+A+Comp:AÄ1mp MP ; !
+A: N-assia_01 ;


! lyhyt:lyhy (??)
LEXICON A-lyhyt
+A+Der/st:%^SyllBre%^StretchSyll2st AdvManner ; !
+A+Comp:%^SyllBre%^StretchSyll2mp MP ;
+A+Comp:emp MP ;
+A: N-venät_01-Sg ;
+A:is Oblique-Plural ;


! glavnoi 2 syllables (??)
LEXICON A-glavnoi
+A+Der/st:%>%^WCnsst AdvManner ; !
+A+Comp:mp MP ;
+A: A-glavnoi_01 ;

LEXICON A-glavnoi_01
 SG-NOM  ; ! glavnoi
 SG-GEN  ; ! glavnoin
 SG-PAR_tA ; ! prostoita
+Sg+Par:%>AÄ1	#   ; ! ommaa
 SG-ILL_hV   ; ! glavnoihe
 SG-INE   ; ! mAÄ1ooz
 SG-ELA   ; ! maost
 SG-ALL   ; ! maolle
 SG-ADE   ; ! maool
 SG-ABL   ; ! maolt
 SG-TRA   ; ! maoks
 SG-ESS_nnA   ; ! mattoon
 SG-EXE   ; ! mattoont
 SG-COM   ;
 SG-ABE	  ;
! Plural
+Pl+Nom:%>%^WCnst     #   ; ! maot
+Pl+Gen:%>i%>n	#   ; ! matoloin
+Pl+Par:%>i%>AÄ1	#   ; ! matoloja
+Pl+Ill:%>i%>hV1	#   ; ! kalattommii
+Pl+Ine:%>i%>z    #   ; ! matoloiz
+Pl+Ela:%>i%>st    #   ; ! matoloist
+Pl+All:%>i%>lle    #   ; ! matoloille
+Pl+Ade:%>i%>l    #   ; ! matoloil
+Pl+Abl:%>i%>lt    #   ; ! matoloilt
+Pl+Tra:%>i%>ks    #   ; ! matoloiks
+Pl+Ess:%>i%>n    #   ; ! matoloinna
+Pl+Exe:%>i%>nt    #   ; ! matoloint

! kalatoin (??)
LEXICON A-kalatoin
+A+Der/st:tOÖ1mAÄ1%>%^WCnsst AdvManner ; !
+A: A-kalatoin_01 ;

LEXICON A-kalatoin_01 
+Sg+Nom:OÖ1in     #   ; ! kalatoin
+Use/Sub+Sg+Par:%>OÖ1in%>t # ;
:OÖ1in SG-PAR_tA   ; ! kalatointa
:tOÖ1mAÄ1 SG-ILL_V   ; ! kalattommAÄ1a
:tOÖ1mAÄ1 Oblique-Singular ;
! Plural
:%>tOÖ1mAÄ1 PL-NOM   ; ! maot
:tOÖ1m PL-PAR_A-LCns   ; ! matoloja
:tOÖ1m PL-ILL_V   ; ! kalattommii
:tOÖ1m Oblique-Plural ;

! kärpäin (XIII)
! formerly A-erilaajain
! punain:punai 
!   2      3
LEXICON A-punain
+A+Der/st:se%>%^WCnsst AdvManner ; !
+A+Comp:semp MP ;
+A:  N-kärpäin_01 ;

! This needs a two-syllable
! entiin:enti
!   2     3 
LEXICON A-entiin
+A+Der/st:se%>%^WCnsst AdvManner ; !
+A+Comp:semp MP ;
+A: N-hoomiin_01 ;

! pohjallin:pohjalli
!    3        4
LEXICON A-pohjallin
+A+Der/st:se%>%^WCnsst AdvManner ; !
+A+Comp:semp MP ;
+A: N-mehiläin_01 ;

! kelvolliin:kelvolli
!    3        4
LEXICON A-kelvolliin
+A+Der/st:se%>%^WCnsst AdvManner ; !
+A+Comp:semp MP ;
+A: N-inmihiin_01 ;



! vahva:vahv ()
LEXICON A-vahva
+A+Der/st:a%>%^WCnsst AdvManner ; !
+A:e%>%^WCnsmp MP ; ! 2-syllables
+A:  N-jalka_01 ;

! matala: matal ()
LEXICON A-matala
+A+Der/st:a%>%^WCnsst AdvManner ; !
+A:amp MP ; ! 3-syllables
+A: N-vokala_01 ;


!paremp
LEXICON A-paremp
+A+Comp: MP ;

LEXICON MP
+Der/st:AÄ1%>%^WCnsst AdvManner ; !
+Sg+Nom:     #   ; ! oma
:AÄ1 SG-PAR_A  ; ! ommaa
:AÄ1 SG-ILL_V   ; ! harvempaa
:AÄ1 MP-Oblique ;

LEXICON MP-Oblique
: Oblique-Singular ;
! Plural
 PL-NOM ;
 PL-PAR_A   ; ! matoloja
 PL-ILL_V   ; ! kovempii
 Oblique-Plural ;


LEXICON Attr-eri
+Attr: # ;

! vim: set ft=xfst-lexc:

