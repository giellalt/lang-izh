!! =================================== !
!! The Ingrian morphophonological/twolc rules file !
!! =================================== !


Alphabet
a b d e f g h i j k l m n o p r s š t u v z ž ü ä ö
A B D E F G H I J K L M N O P R S Š T U V Z Ž Ü Ä Ö
ş ƶ y
Ş Ƶ Y


! Vowel harmony with "(t)a/ä"
AÄ1:a
AÄ1:ä

! Vowel harmony with "loi/löi" also kalatOÖ1in
OÖ1:o
OÖ1:ö

! this appears in the illative
V1:a
V1:e
V1:o
V1:u
V1:y
V1:ä
V1:ö


%- !Hyphen in  constructions 
%>:0
#   ! Word boundary for both lexicalised and dynamic compounds


%^LVws:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0


;


Sets

VwsBack = a o u ;
VwsFront = ä ö y ü ;
VwsNeutral = e i ; 
Vws = a o u ä ö y ü e i ;

Cns = b d f g h j k l m n p r s ş š t v z ƶ ž ;

Letters = Vws Cns ;



Definitions

! Front Trigger
FT = [ #: [ Cns | VwsNeutral ]+ | :VwsFront [ Cns | VwsNeutral ]*  | VwsFront:0 [ Cns | VwsNeutral ]* ] ( %>:0 ) ( ( %^AtoO:0 ) i: ) ;

! Back Trigger 
BT =  [ :VwsBack [ Cns | VwsNeutral]* | VwsBack:0 [ Cns | VwsNeutral]* ] ( %>:0 ) ( ( %^AtoO:0 ) i: ) ;

! Short vowel
SV = [ [ #: | Cns+ ] :Vws ] ;

Rules


! Stem vowel loss before plural "i"
"StemVowLoss"
 X:Y <=> Vws _  %>:0 [ i | i:j ] %>:0 ;
          where X in (a e o u y ü ö ä)
                Y in (0 0 0 0 0 0 0 0) matched ;

!€ maa%>i%>n
!€ ma00i0n

! Stem vowel "i" loss before plural "i"
"i:Zero"
 i:0 <=> Vws _ %>:0 i: %>:0 ;

! iToj between vowels
"i:j"
 i:j <=> :Vws :0* _ %>:0 [ AÄ1:a | AÄ1:ä ] ;
         Vws _ Cns:0 Vws: ;

!€ em0o0i%>%^LCnsAÄ1
!€ emmooj00a

!
! Vowel harmony suffixes Front
"AÄ1:ä"
 AÄ1:ä <=>  FT %>:0 ( %^LCns: | %^LCns: %^LVws: | v v | t | n n | m p: | n k )  _ ;!

! mato : mattooa
! Vowel harmony suffixes Back
"AÄ1:a"
 AÄ1:a <=>  BT %>:0 (  %^LCns: | %^LCns: %^LVws: | v v | t | n n | m p: | n k )  _ ;!

!
! Vowel harmony suffixes Front
"OÖ1:ö"
 OÖ1:ö <=>  FT %>:0 l  _ ;!

! Vowel harmony suffixes Back
"OÖ1:o"
 OÖ1:o <=>  BT %>:0 l  _ ;!

! Vws %>:0 %^LCns: V1:
! Vowel agreement in illative 
"V1:aeouüäo"
 V1:Vx <=> Vx %>:0 [ h | ( %^LCns: ) ] _ ;
        where Vx in (a o u ä ö ü) ;

! Vowel agreement in illative e
"V1:e"
 V1:e <=> [ e | i ] %>:0 [ h | ( %^LCns: ) ] _ ;


! ! Vowel Lengthening
! mato : mattoon, mato : maool
 "Zero:Vx"
 0:Vx <=> Vx _ %>:0 ( %^LCns: | %^WCns: ) %^LVws: ;
         ~[ Cns | Vws ] Cns* Vws Cns 0: Vx _ Cns :0 Vws [ Cns | ~[ Cns | Vws ] ] ;
       where Vx in (a o u ä ö y e) ; 


! Secondary Consonant lengthening
! mato : mattoo, näytettävä : näytettävvää
!"even-syllable" This should be secondary
"consonant lengthening"
 0:Cx <=> ~[ Cns | Vws ][ Cns* Vws | Cns* Vws+ Cns+ Vws+ Cns+ Vws ] Cx _ Vws [ 0:Vws %>:0 %^LCns: | %>:0 %^LCns: :0* :Vws ] ;
! 0:Cx <=>  SV ( Vws* Cns+ Vws+ Cns+ Vws ) Cx _ Vws [ [ :0+  :Vws ] - [ %>:0 i: ] ] ;
! 0:Cx <=> ~[ Cns | Vws ][ Cns* Vws | Cns* Vws+ Cns+ Vws+ Cns+ Vws ] Cx _ Vws ( 0: ) %>:0 %^LCns:0  ;
           where Cx in (v m t s n r l k g h) ;

! oja: oija
"j lengthening"
0:i <=> ~[ Cns | Vws ] Cns* Vws _ j Vws ( 0: ) %>:0 %^LCns:0  ;
          i _ %>:0 ( %^LCns: | %^WCns: ) %^LVws: ;
         ~[ Cns | Vws ] Cns* Vws Cns 0: i _ Cns :0 Vws [ Cns | ~[ Cns | Vws ] ] ;
!!!!!!!!!!!!!!!!!!!!!!!!!
!! Consonant weakening !!
!!!!!!!!!!!!!!!!!!!!!!!!!
! t:0, k:0 (suku:suun), p:v, ht:h, hk:h, lt:ll, rt:rr
! mp:mm, nt:nn, st:ss,
! it:ij, lk:l, rk:r, nk:ng, sk:s, tk:t, 
! kk:k, tt:t, pp:p
! ut:vv, yt:vv (uut:uuvv), uk:vv
! aika:ajan, poika:pojan, reikä:reijän, liika:liijan
! lp:lv, rp:rv


"rt:rr weakening"
t:r <=> ~[ Cns | Vws ] Cns* Vws r _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"lt:ll weakening"
t:l <=> ~[ Cns | Vws ] Cns* Vws l _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"nt:nn weakening"
t:n <=> ~[ Cns | Vws ] Cns* Vws n _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"mp:mm weakening"
p:m <=> ~[ Cns | Vws ] Cns* Vws m _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"nk:ng weakening"
k:g <=> ~[ Cns | Vws ] Cns* Vws n _ :Vws ( 0: ) %>:0 %^WCns:0 ;


! aika: ajan
"kToZero"
k:0 <=> [ #: ] Cns* Vws ( k | i: | t ) _ Vws: ( 0: | %>:0 i: ) %>:0 %^WCns:0 ;



! mato : maot, mato : maool
! tToZero in weak
"Double stop:weakens tToZero"
t:0 <=> ~[ Cns | Vws ] [ Cns* Vws ( t ) | Cns* Vws+ Cns+ Vws+ Cns+ Vws+ ( n ) t ] _ :Vws ( 0: ) %>:0 %^WCns:0 ;


! lehti : lehen
"p:v"
p:v <=> ~[ Cns | Vws ] Cns* Vws _ :Vws ( 0: ) %>:0 %^WCns:0 ;



!!
!"Disallow WLoss after non-v"
!WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %>:0 _ ;

