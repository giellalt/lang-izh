!! =================================== !
!! The Ingrian morphophonological/twolc rules file !
!! =================================== !


Alphabet
a b d e f g h i j k l m n o p r s š t u v z ž ü ä ö
A B D E F G H I J K L M N O P R S Š T U V Z Ž Ü Ä Ö
ş ƶ y
Ş Ƶ Y


! Vowel harmony with "(t)a/ä"
AÄ1:a
AÄ1:ä
AÄ1:0

! Vowel harmony with "loi/löi" also kalatOÖ1in
OÖ1:o
OÖ1:ö

! Vowel harmony with "ttu/tty"
UY1:u
UY1:y

! this appears in the illative
V1:a
V1:e
V1:i
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a
V3:e
V3:i
V3:o
V3:u
V3:y
V3:ä
V3:ö
V3:0

%- !Hyphen in  constructions 
%>:0
#:0   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVws:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0

;


Sets

VwsBack = a o u ;
VwsFront = ä ö y ü ;
VwsNeutral = e i ; 
Vws = a o u ä ö y ü e i ;

Cns = b d f g h j k l m n p r s ş š t v z ƶ ž ;

Letters = Vws Cns ;



Definitions

! Front Trigger
FT =  #: Cns* [ VwsFront | VwsNeutral ] \[ #: | VwsBack: | :VwsBack ]*  ;


! Back Trigger 
BT =  #: Cns* ( VwsNeutral \[ #: | VwsFront: | :VwsFront ]* ) [ VwsBack: | :VwsBack ]  \[ #: | VwsFront: | :VwsFront ]* ;


! Short vowel
SV = [ [ #: | Cns+ ] :Vws ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ #: Cns* Vws : Cns:+ | Cns* Vws+ Cns:+  Vws:+ Cns:+  ] ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vws [ ( %>:0 %^LCns:0 %^LVws:0 | %>:0 %^LCns:0 | %>:0 i %>:0 | %>:0 ) [ V1:Vws | AÄ1: ] |  0:Vws %>:0 ( %^LCns:0 | %^WCns: ) %^LVws: | %>:0 %^LCns:0  ] ] ;

Rules


! Stem vowel loss before plural "i"
"StemVowLoss"
 X:Y <=> X _  ( %>:0  %^WCns:0 |  %>:0  %^LCns:0 ) %>:0 [ i | i:j ] ;
          where X in (e o ö)
                Y in (0 0 0) matched ;

! A loss
"StemALoss"
 X:Y <=> [ X | :Cns ] _  ( %>:0  %^WCns:0 |  %>:0  %^LCns:0 ) %>:0 [ i | i:j ] ;
          where X in (a ä AÄ1)
                Y in (0 0 0) matched ;
!%>%^LCnsi%>V1


! U loss [u:0 | y:0]
"StemULoss"
 X:Y <=> X _  ( %>:0  %^WCns:0 |  %>:0  %^LCns:0 ) %>:0 [ i | i:j ] ;
         [ ä | a | o | ö ] _  [ t: | k: ] 0:v ;
          where X in (u y ü)
                Y in (0 0 0) matched ;
!Vws:0 %>:0  %^WCns:0 %>:0 i: %>:0 

!€ maa%>i%>n
!€ ma00i0n

! Stem vowel "i" loss before plural "i"
"i:Zero"
 i:0 <=> i _ %>:0 i: %>:0 ;

! iToj between vowels
"i:j"
 i:j <=> :Vws :0* %>:0 _ %>:0 [ AÄ1:a | AÄ1:ä ] ;
         Vws _ Cns:0 Vws: ;

!€ em0o0i%>%^LCnsAÄ1
!€ emmooj00a

!
! Vowel harmony suffixes Front
"AÄ1:ä"
 AÄ1:ä <=> FT _  ( %>:0  %^WCns:0 ( %^LVws:0 ) |  %^LVws:0  |  %>:0  %^LCns:0 ( %^LVws:0 ) ) [ ( %>:0 ) [ Cns | V1: | V3: | %^WCns:0 [ Cns | V1: | V3: ] | [[ Vws ] - [ i | i:j ]]] | #: | AÄ1: | %^StretchSyll2:0 ]  ;

! mato : mattooa
! Vowel harmony suffixes Back
"AÄ1:a"
 AÄ1:a <=> BT _   ( %>:0  %^WCns:0 ( %^LVws:0 ) |  %^LVws:0  |  %>:0  %^LCns:0 ( %^LVws:0 ) ) [ ( %>:0 ) [ Cns | V1: | V3: | %^WCns:0 [ Cns | V1: | V3: ] | [[ Vws ] - [ i | i:j ]]] | #: | AÄ1: | %^StretchSyll2:0 ]  ;

!
! Vowel harmony suffixes Front
"OÖ1:ö"
 OÖ1:ö <=>  FT _ ;

! Vowel harmony suffixes Back
"OÖ1:o"
 OÖ1:o <=>  BT _ ;

!
! Vowel harmony suffixes Front
"UY1:y"
 UY1:y <=>  FT _ ;

! Vowel harmony suffixes Back
"UY1:u"
 UY1:u <=>  BT _ ;

! Vws %>:0 %^LCns: V1:
! Vowel agreement in illative 
"V1:aeouüäö"
 V1:Vx <=> :Vx %>:0 [ h | ( %^LCns: %>:0 ) ] _ ;
        where Vx in (a o u ä ö ü y) ;

! Vowel agreement in illative e
"V1:e"
 V1:e <=> [ e | i ] %>:0  h  _ ;
          e ( %>:0 %^LCns: ) %>:0  _ ;

! Illative i with no intermittent _h_
"V1:i"
 V1:i <=> i ( %>:0 %^LCns: ) %>:0 _ ;

!  _mato:maool_ not allowed
! Vowel lengthening for the inesive and adessive with V2
! Used with patsas type nominals
"V2:aeiouüäö"
 V2:Vx <=> [ IL ] Vx ( h ( :h ) ) %>:0  ( %^LCns:0 | %^WCns: )  _ [ V2: | ( %>:0 )  Cns ] ;
           [ IL ] Vx ( h ( :h ) ) %>:0  ( %^LCns:0 | %^WCns: ) V2: _ [ ( %>:0 )  Cns ]  ;
        where Vx in (a o u ä ö ü e i y) ;

!! This deals with secondary or perhaps pertary vowel lengthening
! e.g. gen plural, ine, ade
"V3:a"
 V3:a <=> :Cns :0* :a :0* _ ;

"V3:e"
 V3:e <=> :Cns :0* :e :0* _ ;

"V3:i"
 V3:i <=> :Cns :0* :i :0* _ ;

"V3:o"
 V3:o <=> :Cns :0* :o :0* _ ;

"V3:u"
 V3:u <=> :Cns :0* :u :0* _ ;

"V3:ä"
 V3:ä <=> :Cns :0* :ä :0* _ ;

"V3:ö"
 V3:ö <=> :Cns :0* :ö :0* _ ;

"V3:y"
 V3:y <=> :Cns :0* :y :0* _ ;


"V2:Zero"
 V2:0 <=> [ #: Cns* Vws Cns: ] Vws %>:0  ( %^WCns: ) _ [ s | l ] ;
          Vws %>:0 _ %> i: %> ;

"V3:Zero"
 V3:0 <=> :Vws :0* :Vws :0* _ ;

! ! Vowel Lengthening
! mato : mattoon,
 "Zero:Vx"
 0:Vx <=> ( Vws+ :Cns+ ) Vx _ %>:0 ( %^LCns: | %^WCns: ) %^LVws: ;
          #: Cns* Vws Cns 0: Vx _ Cns ( Cns:0 ) :Vws %^StretchSyll2:0 ; ! ommeena<omena
! 
         [ #: ] Cns* Vws Cns 0: Vx _ Cns :0 Vws [ Cns | [ #: ] ] ;
       where Vx in (a o u ä ö y e) ; 


! Secondary Consonant lengthening
! mato : mattoo, näytettävä : näytettävvää
!"even-syllable" This should be secondary
"consonant lengthening"
 0:Cx <=> 
         [ #: Cns* Vws+ Cns+ Vws+ Cns+ Vws | #: Cns*  Vws | %^OddSyll:0 ] Cx _ ( ( [ Vws: | :Vws ] ( :Vws ) ) %>:0 ) %^LCns:0 ; !
         #: Cns* Vws Cx _ [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! ommeena<omena ; venät:vennäen
           where Cx in (m t s n r l k g h) ;

! oja: oija
"j lengthening"
0:i <=> [ #: ] Cns* Vws _ j Vws ( 0: ) %>:0 %^LCns:0  ;
          [ [ :Cns | :Cns Vws:0 ] ( %>:0 ) | Cns: ] i _ %>:0 ( %^LCns: | %^WCns: ) %^LVws: ;
         #: Cns* Vws Cx _ Vws :Vws Cns ( Cns:0 ) :Vws %^StretchSyll2:0 ;
         [ #: ] Cns* Vws Cns 0: i _ Cns :0 Vws [ Cns | [ #: ] ] ;
!!!!!!!!!!!!!!!!!!!!!!!!!
!! Consonant weakening !!
!!!!!!!!!!!!!!!!!!!!!!!!!
! t:0, k:0 (suku:suun), p:v, ht:h, hk:h, lt:ll, rt:rr
! mp:mm, nt:nn, st:ss,
! it:ij, lk:l, rk:r, nk:ng, sk:s, tk:t, 
! kk:k, tt:t, pp:p
! ut:vv, yt:vv (uut:uuvv), uk:vv
! aika:ajan, poika:pojan, reikä:reijän, liika:liijan
! lp:lv, rp:rv



"Ut:vv Uk:vv weakening"
 Cx:Cy <=> [ #: ] Cns* Vws [ u: | y: ] _ 0: :Vws ( 0: ) %>:0 %^WCns:0 ;
              where Cx in (k t)
                    Cy in (v v) matched ;

"0:v Lengthening with Ut:vv weakening"
 0:v <=> [ #: ] Cns* Vws [ u: | y: ] [ t: | k: ]  _ :Vws ( 0: ) %>:0 %^WCns:0 ; ! löytää:lövvän, 
         [ #: ][ Cns* Vws | Cns* Vws+ Cns+ Vws+ Cns+ :Vws ] v _ :Vws [ 0:Vws %>:0 %^LCns: | %>:0 %^LCns: :0* :Vws ] ; ! arvattava:arvattavvaa
        #: Cns* Vws Cx _ Vws %^SyllBr:0 e %^StretchSyll2:0 ; ! kevät:kevväen
! viis: viijes
"it:j"
 t:j <=> i i _ e %>: %^WCns: ;

"st:st weakening"
t:s <=> Vws+ s _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"rt:rr weakening"
t:r <=>  Vws r _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"lt:ll weakening"
t:l <=>  Vws l _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"nt:nn weakening"
t:n <=>  Vws n _ :Vws ( 0: ) %>:0 %^WCns:0 ;

!! p

"mp:mm weakening"
p:m <=> [ Vws | Vws+ :Cns+ Vws  %^SyllBr:0 e %^StretchSyll2:0 ] m _ :0* [ :Vws | Vws: ] :0* %^WCns:0  ;
! [ ( %>:0 ) :Vws ( 0: ) %>:0 %^WCns:0 | Vws:0 %>:0 i: %>:0 %^WCns:0 | Vws:0 %>:0  %^WCns:0 %>:0 i: %>:0 ] ;

!! k

"nk:ng weakening"
k:g <=> [ #: ] Cns* Vws n _ :Vws ( 0: ) %>:0 %^WCns:0 ;


! aika: ajan
"kToZero"
k:0 <=> [ [ #: ] Cns* Vws (( r | i ) k | t | h | l | r | s ) | Vws :Cns+ :Vws+ k ] _ [ Vws: | :Vws ] ( 0: | %>:0 i: | %^StretchSyll2:0 ) %>:0 %^WCns:0 ;
        [ #: ] Cns* VwsBack i: _ Vws: ( 0: | %>:0 i: ) %>:0 %^WCns:0 ;

! liika:liijan, reikä:reijän
"kToj"
 k:j <=> [ #: ] Cns* [ VwsFront | VwsNeutral ] i: _ Vws: ( 0: | %>:0 i: ) %>:0 %^WCns:0 ;

! mato : maot, ! mato : maool
! tToZero in weak
"Double stop:weakens tToZero"
t:0 <=> [ #: Cns* Vws ( h | t ) | #: Cns* Vws [ Vws - [ i | u | y ] ] | #: Cns*  Vws+ [ h | t ] | Vws ( %>:0 %^WCns:0 ) t | Cns* Vws+ Cns+ Vws+ ( n ) t ] _  [ :Vws | ( %>:0 ) AÄ1: ] ( 0: ) %>:0 %^WCns:0 ;


! lupa : luvan
"p:v"
p:v <=> [ #: ] Cns* Vws ( r | l ) _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"double pp to p"
p:0 <=> [ #: Cns* Vws p | #: Cns* Vws Vws p | Cns* Vws+ Cns+ Vws+ p ] _  [ :Vws | AÄ1: ] ( 0: ) %>:0 %^WCns:0 ;



!!
!"Disallow WLoss after non-v"
!WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %>:0 _ ;

!"Disallow V3 after two consecutive realized vowels"
! V3: /<= :Vws :0* :Vws :0* _ ;