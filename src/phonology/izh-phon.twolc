!! =================================== !
!! The Ingrian morphophonological/twolc rules file !
!! =================================== !


Alphabet
a b d e f g h i j k l m n o p r s š t u v z ž ü ä ö
A B D E F G H I J K L M N O P R S Š T U V Z Ž Ü Ä Ö
ş ƶ y
Ş Ƶ Y


! Vowel harmony with "(t)a/ä"
AÄ1:a
AÄ1:ä

! Vowel harmony with "loi/löi" also kalatOÖ1in
OÖ1:o
OÖ1:ö

! Vowel harmony with "ttu/tty"
UY1:u
UY1:y

! this appears in the illative
V1:a
V1:e
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö


%- !Hyphen in  constructions 
%>:0
#:0   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVws:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0


;


Sets

VwsBack = a o u ;
VwsFront = ä ö y ü ;
VwsNeutral = e i ; 
Vws = a o u ä ö y ü e i ;

Cns = b d f g h j k l m n p r s ş š t v z ƶ ž ;

Letters = Vws Cns ;



Definitions

! Front Trigger
FT =  #: Cns* [ VwsFront: | :VwsFront | VwsNeutral ] \[ #: | VwsBack: | :VwsBack ]*  ;
! [ #: [ :Cns | VwsNeutral ]+ | :VwsFront [ :Cns | VwsNeutral ]*  | VwsFront:0 [ :Cns | VwsNeutral ]* ] ( %>:0 ) ( ( %^AtoO:0 ) i: ) ;

! Back Trigger 
BT =  #: Cns* [ VwsBack: | :VwsBack ] \[ #: | VwsFront: | :VwsFront ]* ;
![ [ :VwsBack | VwsBack:0 ] [ :Cns | VwsNeutral ]* ] ( %>:0 ) ( ( %^AtoO:0 ) i: ) ;

! Short vowel
SV = [ [ #: | Cns+ ] :Vws ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ #: Cns* Vws : Cns:+ | Cns* Vws+ Cns:+  Vws:+ Cns:+  ] ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
SLV = [ :Vws [ ( %>:0 %^LCns:0 %^LVws:0 | %>:0 %^LCns:0 | %>:0 i %>:0 | %>:0 ) [ V1:Vws | AÄ1: ] |  0:Vws %>:0 ( %^LCns: | %^WCns: ) %^LVws: | %>:0 %^LCns:0 [ i %>:0 [ n | i #: ] | i: %>:0 AÄ1: ] ] ] ;

Rules


! Stem vowel loss before plural "i"
"StemVowLoss"
 X:Y <=> Vws _  %>:0 [ i | i:j ] %>:0 ;
          where X in (e o ö)
                Y in (0 0 0) matched ;

! A loss
"StemALoss"
 X:Y <=> [ Vws | :Cns ] _  %>:0 [ i | i:j ] %>:0 ;
          where X in (a ä)
                Y in (0 0) matched ;



! U loss
"StemULoss"
 X:Y <=> Vws _  %>:0 [ i | i:j ] %>:0 ;
         [ ä | a | o | ö ] _  t: 0:v ;
          where X in (u y ü)
                Y in (0 0 0) matched ;


!€ maa%>i%>n
!€ ma00i0n

! Stem vowel "i" loss before plural "i"
"i:Zero"
 i:0 <=> Vws _ %>:0 i: %>:0 ;

! iToj between vowels
"i:j"
 i:j <=> :Vws :0* _ %>:0 [ AÄ1:a | AÄ1:ä ] ;
         Vws _ Cns:0 Vws: ;

!€ em0o0i%>%^LCnsAÄ1
!€ emmooj00a

!
! Vowel harmony suffixes Front
"AÄ1:ä"
 AÄ1:ä <=> FT _ ;

! mato : mattooa
! Vowel harmony suffixes Back
"AÄ1:a"
 AÄ1:a <=> BT _ ;

!
! Vowel harmony suffixes Front
"OÖ1:ö"
 OÖ1:ö <=>  FT _ ;

! Vowel harmony suffixes Back
"OÖ1:o"
 OÖ1:o <=>  BT _ ;

!
! Vowel harmony suffixes Front
"UY1:y"
 UY1:y <=>  FT _ ;

! Vowel harmony suffixes Back
"UY1:u"
 UY1:u <=>  BT _ ;

! Vws %>:0 %^LCns: V1:
! Vowel agreement in illative 
"V1:aeouüäö"
 V1:Vx <=> :Vx %>:0 [ h | ( %^LCns: ) ] _ ;
        where Vx in (a o u ä ö ü y) ;

! Vowel agreement in illative e
"V1:e"
 V1:e <=> [ e | i ] %>:0 [ h | ( %^LCns: ) ] _ ;

!  _mato:maool_ not allowed
! Vowel lengthening for the inesive and adessive with V2
"V2:aeiouüäö"
 V2:Vx <=> [ IL ] Vx %>:0  ( %^WCns: )  _ [ s | l ] ;
        where Vx in (a o u ä ö ü e i y) ;

"V2:Zero"
 V2:0 <=> [ #: Cns* Vws Cns: ] Vws %>:0  ( %^WCns: ) _ [ s | l ] ;

! ! Vowel Lengthening
! mato : mattoon,
 "Zero:Vx"
 0:Vx <=> ( Vws+ Cns+ ) Vx _ %>:0 ( %^LCns: | %^WCns: ) %^LVws: ;
         [ #: ] Cns* Vws Cns 0: Vx _ Cns :0 Vws [ Cns | [ #: ] ] ;
       where Vx in (a o u ä ö y e) ; 


! Secondary Consonant lengthening
! mato : mattoo, näytettävä : näytettävvää
!"even-syllable" This should be secondary
"consonant lengthening"
 0:Cx <=> 
! [ #: ][ Cns* Vws | Cns* Vws+ Cns+ Vws+ Cns+ Vws ] Cx _ :0* [ Vws [ 0:Vws %>:0 %^LCns: | %>:0 %^LCns: :0* :Vws ] | %>: %^LCns: ] ;
        [ #: Cns* | #: Cns* Vws+ Cns+] \[ #: ]*  Vws Cx _ ( ( Vws:0 ) %>:0 ) SLV ;
           where Cx in (m t s n r l k g h) ;

! oja: oija
"j lengthening"
0:i <=> [ #: ] Cns* Vws _ j Vws ( 0: ) %>:0 %^LCns:0  ;
          [ :Cns | :Cns Vws:0 ] ( %>:0 ) i _ %>:0 ( %^LCns: | %^WCns: ) %^LVws: ;
         [ #: ] Cns* Vws Cns 0: i _ Cns :0 Vws [ Cns | [ #: ] ] ;
!!!!!!!!!!!!!!!!!!!!!!!!!
!! Consonant weakening !!
!!!!!!!!!!!!!!!!!!!!!!!!!
! t:0, k:0 (suku:suun), p:v, ht:h, hk:h, lt:ll, rt:rr
! mp:mm, nt:nn, st:ss,
! it:ij, lk:l, rk:r, nk:ng, sk:s, tk:t, 
! kk:k, tt:t, pp:p
! ut:vv, yt:vv (uut:uuvv), uk:vv
! aika:ajan, poika:pojan, reikä:reijän, liika:liijan
! lp:lv, rp:rv



"Ut:vv Uk:vv weakening"
 Cx:Cy <=> [ #: ] Cns* Vws [ u: | y: ] _ 0:v :Vws ( 0: ) %>:0 %^WCns:0 ;
              where Cx in (k t)
                    Cy in (v v) matched ;

"0:v Lengthening with Ut:vv weakening"
 0:v <=> [ #: ] Cns* Vws [ u: | y: ] [ t:v | k:v ]  _ :Vws ( 0: ) %>:0 %^WCns:0 ;
         [ #: ][ Cns* Vws | Cns* Vws+ Cns+ Vws+ Cns+ :Vws ] v _ :Vws [ 0:Vws %>:0 %^LCns: | %>:0 %^LCns: :0* :Vws ] ;

! viis: viijes
"it:j"
 t:j <=> i i _ e %>: %^WCns: ;

"st:st weakening"
t:s <=> [ #: ] Cns* Vws s _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"rt:rr weakening"
t:r <=> [ #: ] Cns* Vws r _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"lt:ll weakening"
t:l <=> [ #: ] Cns* Vws l _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"nt:nn weakening"
t:n <=> [ #: ] Cns* Vws n _ :Vws ( 0: ) %>:0 %^WCns:0 ;

!! p

"mp:mm weakening"
p:m <=> [ #: ] Cns* Vws m _ [ ( %>:0 ) :Vws ( 0: ) %>:0 %^WCns:0 | Vws:0 %>:0 i: %>:0 %^WCns:0 ] ;

!! k

"nk:ng weakening"
k:g <=> [ #: ] Cns* Vws n _ :Vws ( 0: ) %>:0 %^WCns:0 ;


! aika: ajan
"kToZero"
k:0 <=> [ [ #: ] Cns* Vws (( r ) k | t | h | l | r | s ) | Vws Cns+ :Vws+ k ] _ Vws: ( 0: | %>:0 i: ) %>:0 %^WCns:0 ;
        [ #: ] Cns* VwsBack i: _ Vws: ( 0: | %>:0 i: ) %>:0 %^WCns:0 ;

! liika:liijan, reikä:reijän
"kToj"
 k:j <=> [ #: ] Cns* [ VwsFront | VwsNeutral ] i: _ Vws: ( 0: | %>:0 i: ) %>:0 %^WCns:0 ;

! mato : maot, ! mato : maool
! tToZero in weak
"Double stop:weakens tToZero"
t:0 <=> [ #: Cns* Vws ( h | t ) | #: Cns* Vws Vws t | Cns* Vws+ Cns+ Vws+ ( n ) t ] _  [ :Vws | ( %>:0 ) AÄ1: ] ( 0: ) %>:0 %^WCns:0 ;


! lupa : luvan
"p:v"
p:v <=> [ #: ] Cns* Vws ( r | l ) _ :Vws ( 0: ) %>:0 %^WCns:0 ;

"double pp to p"
p:0 <=> [ #: Cns* Vws p | #: Cns* Vws Vws p | Cns* Vws+ Cns+ Vws+ p ] _  [ :Vws | AÄ1: ] ( 0: ) %>:0 %^WCns:0 ;



!!
!"Disallow WLoss after non-v"
!WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %>:0 _ ;

